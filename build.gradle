plugins {
    id 'scala'
    id 'maven-publish'
    id 'signing'
    id 'org.spongepowered.plugin' version '0.9.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply from: "${rootProject.rootDir}/scala.gradle" //Replace with dotty.gradle to enable Scala 3.

group 'io.github.iltotore'
version '0.1'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation scalaLib()
    implementation scalaDependency('io.github.iltotore', 'scalabuilder', '0.1')
    implementation 'org.spongepowered:spongeapi:7.3.0'
}

sponge {
    plugin {
        id = 'sponge-scala'
        meta {
            authors = ['Il_totore']
        }
    }
}

shadowJar {
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
    archiveClassifier = 'plugin'
    from('build/tmp/generateMetadata') //Include mcmod.info
    relocate 'scala', 'io.github.iltotore.spongescala.library' //Avoid Scala version clash with forge (which uses 2.11)
}

Properties localProperties = new Properties()
localProperties.load(rootProject.file('local.properties').newDataInputStream())

for(Map.Entry<String, ?> entry : localProperties.entrySet()){
    project.ext[entry.getKey()] = entry.getValue()
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadocJar {
    from new File(buildDir, 'docs/scaladoc')
    group 'build'
    dependsOn scaladoc
}

scaladoc {
    source project.sourceSets.main.allScala
}

publishing {
    publications {
        mavenScala(MavenPublication) {
            artifactId = 'sponge-scala_' + getMajorVersion(scalaVersion)

            repositories {
                maven {
                    url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    credentials {
                        username = "Il_totore"
                        password = System.getenv("SONATYPE_PASSWORD")
                    }
                }
            }

            pom {
                name = 'ScalaBuilder'
                description = 'Another way to describe case classes in Scala'
                url = 'https://github.com/Iltotore/SpongeScala'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'iltotore'
                        name = 'Il_totore'
                        email = 'rafbodaha@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Iltotore/SpongeScala.git'
                    developerConnection = 'scm:git:ssh://github.com/Iltotore/SpongeScala.git'
                    url = 'https://github.com/Iltotore/SpongeScala.git'
                }
            }

            from components.java
        }
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenScala
}